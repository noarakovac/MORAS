// BS.search(a, b, c)
function BS.search 3
	// THAT = a
	push argument 0
	pop pointer 1

	// low = 0
	push constant 0
	pop local 0

	// high = b
	push argument 1
	pop local 1

label loop
	// if (low >= high) goto loopEnd;
	push local 0
	push local 1
	lt
	not
	if-goto loopEnd

	// mid = (low + high) / 2
	push local 0
	push local 1
	add
	push constant 2
	call Div.div 2
	pop temp 1

	push temp 0
	pop local 2

	// THAT manip
	push argument 0
	push local 2
	add
	pop pointer 1	

	// if (THAT[mid] < c)
	push that 0
	push argument 2
	lt

	if-goto lessThan

	// high = mid
	push local 2
	pop local 1
	goto finishCycle

label lessThan
	// low = mid + 1
	push local 2
	push constant 1
	add
	pop local 0

label finishCycle
	// THAT = a
	push argument 0
	pop pointer 1

	goto loop
	
label loopEnd

	// if (THAT[low] == c)
	push argument 0
	push local 0
	add
	pop pointer 1

	push that 0
	push argument 2
	eq

	if-goto found

	// return -1
	push constant 0
	push constant 1
	sub
	return

label found
	// return low
	push local 0
	return